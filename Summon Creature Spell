short playonce
ref SUMN

Begin ScriptEffectStart

;Set temp reference for scripting. 
;Allows faster transition of script as template. Also allows for leveled summon.
    set SUMN to MiBryonneREF
    ;Reset our creature if re-summoned before time runs out on spell                                            
    SUMN.disable        

    ;Move creature to the Player
    ;Move the creature reference to the worldspace of the Player
    SUMN.moveto Player 30 30 10    
    ;Make creature visible and active    
    SUMN.enable                        

    set fade to 1    ;Resets the alpha fade variable to 1

End

Begin ScriptEffectUpdate

    if ( timer > 45 )    ;Creature is dead and fade timer passed
        ;Move our creature back to its holding cell
        SUMN.kill
        SUMN.moveto zClothesTest 100 100 300 
        ;Reset our creature if dead
        SUMN.resurrect    
        ;Set our creature to an unprocessed state                            
        SUMN.disable                                    
    endif

    ;Adjust timer state for early fade/disable upon death
    if ( SUMN.getdead == 1 ) && ( timer < 43 )                
        set timer to 43
    endif

    if ( timer > 0.1 ) && ( playonce == 0 )
        ;Play effect for creature entrance
        SUMN.pms effectSummonMythicDawn 1            
        set playonce to 1
    endif

    
    ;Increment timer
    set timer to timer + GetSecondsPassed        


    if ( timer > 43 ) && ( playonce == 1 )
        ;Play effect for creature exit/death
        SUMN.kill            
        set playonce to 2
    endif

    if ( playonce == 2 )    
        ;Fade creature for exit/death                                            
        set fade to fade - 0.03
        SUMN.saa fade
    endif
        

End

Begin ScriptEffectFinish

    ;Move creature back to its holding cell
    SUMN.moveto zClothesTest 100 100 300    
    ;Reset our creature if dead                        
    SUMN.resurrect        
    ;Set our creature to an unprocessed state                                        
    SUMN.disable                                                    

End
